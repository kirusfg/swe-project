plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.node-gradle.node' version '3.1.1'
    id 'java'
    id 'war'
}

group = 'kz.edu.nu'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.1'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat:2.6.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.1'
    developmentOnly 'org.springframework.boot:spring-boot-devtools:2.6.1'
    runtimeOnly 'org.postgresql:postgresql:42.3.1'
}

test {
    useJUnitPlatform()
}

node {
    version = '16.13.0'
    yarnVersion = '1.22.17'

    download = true

    workDir = file("${project.buildDir}/yarn")
    npmWorkDir = file("${project.buildDir}/yarn")
}

task frontendInstall(type: YarnTask) {
    workingDir = file("${project.projectDir}/frontend")
    args = ["install"]
}

task frontendBuild(type: YarnTask) {
    workingDir = file("${project.projectDir}/frontend")
    args = ["run", "build"]
}

task copyResources(type: Copy) {
    from "frontend/build"
    into "build/resources/main/static"
}

frontendBuild.dependsOn frontendInstall
copyResources.dependsOn frontendBuild
build.dependsOn copyResources